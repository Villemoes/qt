#
# QMake variables for Qt4
#
CLASS_DEPENDS += "native:qmake"
QMAKESPEC = "${BUILD_SYSROOT}${stage_datadir}/qt4/mkspecs/g++"
QMAKESPEC:native = ""
export OE_QMAKE_UIC = "uic"
export OE_QMAKE_MOC = "moc"
export OE_QMAKE_RCC = "rcc"
export OE_QMAKE_QDBUSCPP2XML = "qdbuscpp2xml"
export OE_QMAKE_QDBUSXML2CPP = "qdbusxml2cpp"
export OE_QMAKE_LRELEASE = "lrelease"
export OE_QMAKE_LUPDATE = "lupdate"
export OE_QMAKE_QMAKE = "qmake"
export OE_QMAKE_CC="${CC}"
export OE_QMAKE_CFLAGS="${CFLAGS}"
export OE_QMAKE_CXX="${CXX}"
export OE_QMAKE_LINK = "${CXX}"
export OE_QMAKE_LDFLAGS="${LDFLAGS}"
export OE_QMAKE_AR="${AR}"
export OE_QMAKE_STRIP="echo"
export OE_QMAKE_CXXFLAGS = "${CXXFLAGS}"
#export OE_QMAKE_RPATH="-Wl,-rpath-link,"

# do not export STRIP to the environment
STRIP[unexport] = "1"
QT_DIR_NAME = "qtopia"
QT_CONF_PATH = "${SRCDIR}/qt.conf"
export QT_CONF_PATH
do_configure[prefuncs] += "do_configure_conf"
do_configure_conf() {
    echo "[Paths]"                                 > $QT_CONF_PATH
    echo "Prefix=${prefix}/"                      >> $QT_CONF_PATH
    echo "Documentation=${docdir}/${QT_DIR_NAME}" >> $QT_CONF_PATH
    echo "Headers=${includedir}/${QT_DIR_NAME}"   >> $QT_CONF_PATH
    echo "Libraries=${libdir}"                    >> $QT_CONF_PATH
    echo "Binaries=${bindir}"                     >> $QT_CONF_PATH
    echo "Plugins=${libdir}/${QT_DIR_NAME}/plugins" >> $QT_CONF_PATH
    echo "Data=${datadir}/${QT_DIR_NAME}"         >> $QT_CONF_PATH
    echo "Translations=${datadir}/${QT_DIR_NAME}/translations" >> $QT_CONF_PATH
    echo "Settings=${sysconfdir}/${QT_DIR_NAME}"  >> $QT_CONF_PATH
    echo "Examples=${bindir}/${QT_DIR_NAME}/examples" >> $QT_CONF_PATH
    echo "Demos=${bindir}/${QT_DIR_NAME}/demos"   >> $QT_CONF_PATH
}

install_conf () {
    if [ -z "${OE_QMAKE_CXX}" ]; then
        echo "ERROR: cannot install $1 to $2, OE_QMAKE var not set"
        exit 1
    fi
    cp -vf "$1" "$2"
    sed -i -e 's#\$\((OE_QMAKE_CXX)\)#${OE_QMAKE_CXX}#' \
        -i -e 's#\$\((OE_QMAKE_CC)\)#${OE_QMAKE_CC}#' \
        -i -e 's#\$\((OE_QMAKE_LINK)\)#${OE_QMAKE_LINK}#' \
        -i -e 's#\$\((OE_QMAKE_LDFLAGS)\)#${OE_QMAKE_LDFLAGS}#' \
        -i -e 's#\$\((OE_QMAKE_CFLAGS)\)#${OE_QMAKE_CFLAGS}#' \
        -i -e 's#\$\((OE_QMAKE_CXXFLAGS)\)#${OE_QMAKE_CXXFLAGS}#' \
        "$2"
}
